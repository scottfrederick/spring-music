apply plugin: 'java'

apply plugin: 'eclipse-wtp'
apply plugin: 'idea'

apply plugin: 'tomcat'
apply plugin: 'war'

apply plugin: 'cloudfoundry'

sourceCompatibility = 1.6
version = '1.0'

sourceSets {
    main {
        java {
            srcDir 'src/main/java'
        }
        resources {
            srcDir 'src/main/resources'
            srcDir 'src/main/java'
        }
    }
}

jar {
    manifest {
        attributes 'Implementation-Title': 'Spring Sample Application', 'Implementation-Version': version
    }
}

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'org.gradle.api.plugins:gradle-tomcat-plugin:0.9.8'
        classpath 'org.cloudfoundry:cf-gradle-plugin:1.0.0'
    }
}

repositories {
    mavenCentral()
    mavenLocal()
    maven { url "http://repo.springsource.org/milestone" }
}

dependencies {
    // Spring and dependencies
    compile "org.springframework:spring-context:${springVersion}"
    compile "org.springframework:spring-webmvc:${springVersion}"
    compile 'org.codehaus.jackson:jackson-mapper-asl:1.9.8'

    // Spring Cloud
    compile 'org.springframework.cloud:spring-service-connector:0.9.2'
    compile 'org.springframework.cloud:cloudfoundry-connector:0.9.2'

    // Spring Data
    compile 'org.springframework.data:spring-data-jpa:1.3.2.RELEASE'
    compile 'org.springframework.data:spring-data-redis:1.0.4.RELEASE'
    compile 'org.springframework.data:spring-data-mongodb:1.2.1.RELEASE'

    // JPA Persistence
    compile "commons-dbcp:commons-dbcp:1.2.2"
    compile 'org.hibernate:hibernate-entitymanager:4.1.9.Final'
    runtime 'com.h2database:h2:1.3.162'
    runtime 'mysql:mysql-connector-java:5.1.25'
    runtime 'postgresql:postgresql:9.1-901-1.jdbc4'

    // JSR-303 validation
    compile 'javax.validation:validation-api:1.0.0.GA'
    compile 'org.hibernate:hibernate-validator:4.3.0.Final'

    // Logging
    compile 'org.slf4j:slf4j-api:1.7.2'
    compile 'org.slf4j:slf4j-log4j12:1.7.2'

    // Servlet
    compile "javax.servlet:jstl:1.2"
    providedCompile 'javax.servlet:javax.servlet-api:3.0.1'

    // Testing
    testCompile 'junit:junit:4.8.2'

    // Spring Testing
    testCompile "org.springframework:spring-test:${springVersion}"

    // Tomcat
    tomcat "org.apache.tomcat.embed:tomcat-embed-core:${tomcatVersion}",
           "org.apache.tomcat.embed:tomcat-embed-logging-juli:${tomcatVersion}"
    tomcat("org.apache.tomcat.embed:tomcat-embed-jasper:${tomcatVersion}") {
        exclude group: 'org.eclipse.jdt.core.compiler', module: 'ecj'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.8'
}

war {
    // omit the version from the war file name
    version = ""
}

tomcatRun {
    outputFile = file('tomcat.log')
}

cloudfoundry {
    target = "https://api.run.pivotal.io"
    space = "development"
    file = file("build/libs/spring-music.war")
    uri = "spring-music-${randomWord}.cfapps.io"
    memory = 512
    instances = 1
    
    serviceInfos {
        "music-mongodb" {
            label = "mongolab"
            provider = "mongolab"
            version = "n/a"
            plan = "sandbox"
            bind = true
        }
    }
}

